      *================================================================*00001006
       IDENTIFICATION                  DIVISION.                        00002003
      *================================================================*00003006
       PROGRAM-ID.                     F2404C3.                         00004003
      *================================================================*00005006
      *  PROGRAMA....:                                                 *00006007
      *  PROGRAMADOR.: RAFAEL DE OLIVEIRA                              *00007007
      *  ANALISTA....: IVAN SANCHES                                    *00008007
      *  CONSULTORIA.: FOURSYS                                         *00009007
      *  DATA........: 09 / 10 / 2024                                  *00010007
      *----------------------------------------------------------------*00020006
      *                                                                *00021007
      *  OBJETIVO....: PROGRAMA TEM A FINALIDADE DE FAZER A CONSULTA   *00030007
      *  DENTRO DE UMA TABELA CICS UTILIZANDO UM ARQUIVO VSAM (VSAMARQ)*00031007
      *                                                                *00032007
      *----------------------------------------------------------------*00111007
      *                                                                *00111107
      *-------------- ESPECIFICACOES DO PROGRAMA ----------------------*00112007
      *                                                                *00112107
      *   ARQUIVO......:                                               *00113007
      *                                                                *00114007
      *   DDNAME.......:       TIPO                 BOOK               *00115007
      *     VSAMARQ             I                  F2404M3             *00116007
      *                                                                *00117007
      *----------------------------------------------------------------*00118007
      *                                                                *00118107
      *   INC'S........:                                               *00119007
      *                                                                *00119107
      *                                                                *00119407
      *----------------------------------------------------------------*00119507
      *                                                                *00119607
      *   MODULOS......:                            BOOK....:          *00119707
      *                                                                *00119807
      *   GRAVALOG                                                     *00119907
      *                                                                *00120007
      *----------------------------------------------------------------*00120107
      *                                                                *00120207
      *----------------- ALTERACOES DO PROGRAMA------------------------*00120307
      *   PROGRAMADOR..:                                               *00120407
      *   ANALISTA.....:                                               *00120507
      *   CONSULTORIA..:                                               *00120607
      *   DATA.........:                                               *00120707
      *   OBJETIVO.....:                                               *00120807
      *----------------------------------------------------------------*00120907
      *                                                                *00121007
      *================================================================*00130006
       ENVIRONMENT                     DIVISION.                        00140003
      *================================================================*00150006
                                                                        00160003
      *================================================================*00170006
       DATA                            DIVISION.                        00180003
      *================================================================*00190006
                                                                        00200003
      *----------------------------------------------------------------*00210006
       FILE                            SECTION.                         00220003
      *----------------------------------------------------------------*00230006
                                                                        00240003
      *----------------------------------------------------------------*00250006
       WORKING-STORAGE                 SECTION.                         00260003
      *----------------------------------------------------------------*00270006
                                                                        00280003
      *----------------------------------------------------------------*00290006
       01  FILLER                      PIC X(050)           VALUE       00300003
           "***** INICIO DA WORKING-STORAGE *****".                     00310003
      *----------------------------------------------------------------*00320006
                                                                        00320106
      *----------------------------------------------------------------*00320206
       01  FILLER                      PIC X(050)           VALUE       00320306
           "***** VARIAVEIS AUXILIAR *****".                            00320406
      *----------------------------------------------------------------*00320506
                                                                        00321003
       01  WRK-RETORNO.                                                 00322003
           05 WRK-CODIGO               PIC X(003)          VALUE SPACES.00323003
           05 WRK-NOME                 PIC X(020)          VALUE SPACES.00324003
           05 WRK-EMAIL                PIC X(020)          VALUE SPACES.00325003
           05 WRK-STREAMING            PIC X(010)          VALUE SPACES.00326003
           05 WRK-VALOR                PIC 9(005)          VALUE ZEROS. 00327004
                                                                        00327106
      *----------------------------------------------------------------*00327206
       01  FILLER                      PIC X(050)          VALUE        00327307
           "***** VARIAVEIS DE APOIO AO ERRO *****".                    00327406
      *----------------------------------------------------------------*00327506
                                                                        00330003
       01  WRK-RET-CICS                PIC S9(004) COMP    VALUE ZEROS. 00340003
       01  WRK-RESP                    PIC S9(004) COMP    VALUE ZEROS. 00351006
                                                                        00351107
      *----------------------------------------------------------------*00351207
       01  FILLER                      PIC X(050)          VALUE        00351307
           "***** VARIAVEIS DE APOIO *****".                            00351407
      *----------------------------------------------------------------*00351507
                                                                        00352006
       01  WRK-FASE                    PIC 9               VALUE 1.     00360003
       01  WRK-CHAVE                   PIC 9(003)          VALUE ZEROS. 00360207
       01  WS-ABSTIME                  PIC S9(015) COMP-3  VALUE ZEROS. 00360307
                                                                        00411603
      *----------------------------------------------------------------*00411707
       01  FILLER                      PIC X(050)          VALUE        00411807
           "***** AREA DE BOOK *****".                                  00411907
      *----------------------------------------------------------------*00412007
                                                                        00412303
           COPY F2404M3.                                                00412403
                                                                        00412503
      *----------------------------------------------------------------*00413006
       01  FILLER                      PIC X(050)          VALUE        00414003
           "***** FIM DA WORKING-STORAGE *****".                        00415003
      *----------------------------------------------------------------*00416006
                                                                        00417003
      *================================================================*00418006
       PROCEDURE                       DIVISION.                        00419003
      *================================================================*00420006
                                                                        00430003
       0000-PRINCIPAL                  SECTION.                         00440003
                                                                        00450003
           PERFORM 0100-INICIALIZAR.                                    00460003
                                                                        00470003
           IF WRK-FASE EQUAL '2'                                        00480003
               PERFORM 0200-PROCESSAR                                   00490003
           END-IF                                                       00500003
                                                                        00510003
           PERFORM 0300-FINALIZAR.                                      00520003
                                                                        00530003
           EXEC CICS                                                    00540003
               RETURN                                                   00550003
               TRANSID('FRA3')                                          00550103
           END-EXEC.                                                    00550203
                                                                        00550303
       0000-PRINCIPAL-FIM.             EXIT.                            00550403
                                                                        00550503
      *----------------------------------------------------------------*00550606
       0100-INICIALIZAR                SECTION.                         00550703
      *----------------------------------------------------------------*00550806
                                                                        00550906
           EXEC CICS RECEIVE                                            00552203
               MAPSET('F2404M3')                                        00552303
               MAP('MAPTEST')                                           00553003
               INTO(MAPTESTI)                                           00554003
               RESP(WRK-RET-CICS)                                       00555003
           END-EXEC.                                                    00556003
                                                                        00557003
           EVALUATE WRK-RET-CICS                                        00558003
                                                                        00559003
               WHEN DFHRESP(NORMAL)                                     00560003
                   MOVE 2              TO WRK-FASE                      00570003
               WHEN DFHRESP(MAPFAIL)                                    00580003
                   MOVE 1              TO WRK-FASE                      00590003
               WHEN OTHER                                               00600003
                   EXEC CICS                                            00610003
                       RETURN                                           00611003
                   END-EXEC                                             00612003
                                                                        00613003
           END-EVALUATE.                                                00614003
                                                                        00615003
       0100-INICIALIZAR-FIM.           EXIT.                            00616003
                                                                        00617003
      *----------------------------------------------------------------*00618006
       0200-PROCESSAR                  SECTION.                         00619003
      *----------------------------------------------------------------*00620006
                                                                        00630003
           MOVE CODIGOI                TO WRK-CODIGO.                   00640003
                                                                        00650003
           EXEC CICS READ                                               00660003
               FILE('VSAMARQ')                                          00660103
               RIDFLD(WRK-CODIGO)                                       00660203
               INTO(WRK-RETORNO)                                        00660303
               RESP(WRK-RET-CICS)                                       00660403
           END-EXEC.                                                    00660503
                                                                        00660603
           IF  WRK-RET-CICS            EQUAL ZEROS                      00660703
               MOVE WRK-CODIGO         TO CODIGOO                       00660803
               MOVE WRK-NOME           TO NOMEO                         00660903
               MOVE WRK-EMAIL          TO EMAILO                        00661003
               MOVE WRK-STREAMING      TO STREAMIO                      00661103
               MOVE WRK-VALOR          TO VALORO                        00661203
               MOVE "--ACHEI--"        TO MSGO                          00662003
           ELSE                                                         00663003
               MOVE SPACES             TO CODIGOO NOMEO EMAILO          00664003
                                          STREAMIO VALORO               00664103
               MOVE "NAO ACHEI"        TO MSGO                          00665003
           END-IF.                                                      00666003
                                                                        00667003
           EVALUATE EIBAID                                              00668003
                                                                        00669003
               WHEN '3'                                                 00670003
                   MOVE 'OBRIGADO'     TO MSGO                          00680003
                   EXEC CICS SEND TEXT                                  00681003
                       FROM(MSGO)                                       00682003
                       ERASE                                            00682103
                       FREEKB                                           00682203
                   END-EXEC                                             00682303
                                                                        00682403
                   EXEC CICS                                            00682503
                       RETURN                                           00682603
                   END-EXEC                                             00682703
                                                                        00682803
           END-EVALUATE.                                                00682903
                                                                        00683003
                                                                        00684003
       0200-PROCESSAR-FIM.             EXIT.                            00685003
                                                                        00686003
      *----------------------------------------------------------------*00687006
       0300-FINALIZAR                  SECTION.                         00688003
      *----------------------------------------------------------------*00689006
                                                                        00689106
                                                                        00691003
           IF WRK-FASE                 EQUAL 1                          00700006
               EXEC CICS SEND                                           00710003
                   MAPSET('F2404M3')                                    00720003
                   MAP('MAPTEST')                                       00730003
                   MAPONLY                                              00731003
                   ERASE                                                00732003
               END-EXEC                                                 00733003
                                                                        00733106
               INITIALIZE MAPTESTO                                      00733206
                                                                        00733306
               EXEC CICS ASKTIME                                        00733406
                   ABSTIME(WS-ABSTIME)                                  00733506
               END-EXEC                                                 00733606
                                                                        00733806
               EXEC CICS FORMATTIME                                     00733906
                   ABSTIME(WS-ABSTIME)                                  00734006
                   DDMMYYYY(DATAO)                                      00734106
                   DATESEP('/')                                         00734206
               END-EXEC                                                 00734306
                                                                        00734406
               EXEC CICS SEND                                           00734506
                   MAPSET('F2404M3')                                    00734606
                   MAP('MAPTEST')                                       00734706
                   DATAONLY                                             00734806
                   FROM(MAPTESTO)                                       00734906
               END-EXEC                                                 00735006
           ELSE                                                         00735103
               EXEC CICS SEND                                           00735203
                   MAPSET('F2404M3')                                    00736003
                   MAP('MAPTEST')                                       00737003
                   DATAONLY                                             00738003
                   FROM(MAPTESTO)                                       00739003
               END-EXEC                                                 00739103
           END-IF.                                                      00739203
                                                                        00739303
       0300-FINALIZAR-FIM.             EXIT.                            00739403
                                                                        00739503
