      *================================================================*
       IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     F2404P05.
      *================================================================*
      *   PROGRAMA.....: F2404P05
      *   PROGRAMADOR..: RAFAEL DE OLIVEIRA
      *   ANALISTA.....: IVAN SANCHES
      *   DATA.........: 22/08/2024
      *   CONSULTORIA..: FOURSYS
      *----------------------------------------------------------------*
      *   OBJETIVO.....: CRIAR PROGRAMA QUE FACA LEITURA E GRAVE
      *                  OS SALDOS VALIDOS E NAO VALIDOS MOSTRANDO
      *                  ESTATISTICA E SALDO TOTAL.
      *
      *----------------------------------------------------------------*
      *-------------  ESPECIFICACOES DO PROGRAMA ----------------------*
      *   ARQUIVO......:
      *
      *   DDNAME.......:       TIPO                 BOOK
      *   EVSA2208             INPUT
      *   SVSA2208             OUTPUT               I#BV2208
      *
      *----------------------------------------------------------------*
      *   INC'S........:
      *   BOOKMSG - VARIAVEIS PARA MENSAGEM DE ERRO
      *   BOOKLOG - AREA FORMATACAO DE ERROS -> GRAVALOG
      *
      *----------------------------------------------------------------*
      *   MODULOS......:                            BOOK....:
      *
      *   GRAVALOG                                  BOOKLOG
      *----------------------------------------------------------------*
      *----------------- ALTERACOES DO PROGRAMA------------------------*
      *   PROGRAMADOR..:
      *   ANALISTA.....:
      *   CONSULTORIA..:
      *   DATA.........:
      *   OBJETIVO.....:
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT                     DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION                   SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT EVSA2208 ASSIGN TO EVSA2208
           FILE STATUS IS FS-EVSA2208.

           SELECT SVSA2208 ASSIGN TO SVSA2208
           FILE STATUS IS FS-SVSA2208.
      *----------------------------------------------------------------*

      *================================================================*
       DATA                            DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                            SECTION.
      *----------------------------------------------------------------*
      * INPUT - DADOS DO ARQUIVO DE ENTRADA (EVSA2208) LRECL = 019
      *----------------------------------------------------------------*
       FD  EVSA2208
           RECORDING MODE IS F.

       01  REG-EVSA2208.
           05 REG-EVSA2208-AGENCIA     PIC X(004).
           05 REG-EVSA2208-CONTA       PIC X(005).
           05 REG-EVSA2208-SALDO       PIC 9(010).

      *----------------------------------------------------------------*
      * OUTPUT - DADOS DO ARQUIVO DE SAIDA (SVSA2208) LRECL = 027

      *----------------------------------------------------------------*

       FD  SVSA2208
           RECORDING MODE IS F.
       COPY 'I#BV2208'.

      *----------------------------------------------------------------*
       WORKING-STORAGE                 SECTION.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- INICIO DA WORKING -----'.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEL DE FILE STATUTS -----'.
      *----------------------------------------------------------------*

       01  FS-EVSA2208                 PIC 9(002)          VALUE ZEROS.
       01  FS-SVSA2208                 PIC 9(002)          VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS   ACUMULADORA ----'.
      *----------------------------------------------------------------*

       01  ACU-LIDOS                   PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-GRAVADOS                PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-DESPREZADOS             PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-TOTAL-GRAVA             PIC 9(011) COMP-3   VALUE ZEROS.
      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEL DE APOIO ----'.
      *----------------------------------------------------------------*

       01  WRK-STREAMING               PIC X(010)          VALUE SPACES.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS DO BOOK-----'.
      *----------------------------------------------------------------*

       COPY BOOKLOG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- MENSAGENS DE ERRO-----'.
      *----------------------------------------------------------------*

       COPY BOOKMSG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- DATA DO SISTEMA -----'.
      *----------------------------------------------------------------*

       01  WRK-DATA-SIST               PIC 9(008)          VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- FIM DA WORKING -----'.
      *----------------------------------------------------------------*

      *================================================================*
       PROCEDURE                       DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       0000-PRINCIPAL                  SECTION.
      *----------------------------------------------------------------*

           PERFORM 0100-INICIALIZAR.

           PERFORM 0140-TESTAR-VAZIO.

           PERFORM 0200-PROCESSAR UNTIL FS-EVSA2208 NOT EQUAL 00.

           PERFORM 0300-FINALIZAR.

       0000-PRINCIPAL-FIM.              EXIT.

      *----------------------------------------------------------------*
       0100-INICIALIZAR                 SECTION.
      *----------------------------------------------------------------*

           OPEN INPUT  EVSA2208
                OUTPUT SVSA2208.

           PERFORM 0110-TESTAR-STATUS.

           ACCEPT WRK-DATA-SIST FROM DATE YYYYMMDD.

       0100-INICIALIZAR-FIM.           EXIT.

      *----------------------------------------------------------------*
       0110-TESTAR-STATUS              SECTION.
      *----------------------------------------------------------------*

       0115-TESTAR-EVSA2208.

           IF FS-EVSA2208 NOT EQUAL ZEROS
              MOVE 'F2404P05'          TO WRK-PROGRAMA
              MOVE 'EVSA2208'          TO WRK-ARQUIVO
              MOVE '0115'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-EVSA2208         TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.

       0120-TESTAR-SVSA2208.

           IF FS-SVSA2208 NOT EQUAL ZEROS
              MOVE 'F2404P05'          TO WRK-PROGRAMA
              MOVE 'SVSA2208'          TO WRK-ARQUIVO
              MOVE '0120'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-EVSA2208         TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.

       0110-TESTAR-STATUS-FIM.         EXIT.

      *----------------------------------------------------------------*
       0130-LER-DADOS                  SECTION.
      *----------------------------------------------------------------*

            READ EVSA2208.

            IF FS-EVSA2208 NOT EQUAL ZEROS AND FS-EVSA2208 NOT EQUAL 10
                MOVE 'F2404P03'        TO WRK-PROGRAMA
                MOVE 'EVSA2208'        TO WRK-ARQUIVO
                MOVE '0130'            TO WRK-SECAO
                MOVE WRK-ERRO-LEITURA  TO WRK-MENSAGEM
                MOVE FS-EVSA2208       TO WRK-STATUS
                PERFORM 9999-TRATA-ERROS
            END-IF.

       0130-LER-DADOS-FIM.             EXIT.

      *----------------------------------------------------------------*
       0140-TESTAR-VAZIO               SECTION.
      *----------------------------------------------------------------*

           PERFORM 0130-LER-DADOS.

           IF FS-EVSA2208 EQUAL 10
              DISPLAY "ARQUIVO VAZIO"
              PERFORM 0300-FINALIZAR
           END-IF.

       0140-TESTAR-VAZIO-FIM.          EXIT.

      *----------------------------------------------------------------*
       0200-PROCESSAR                  SECTION.
      *----------------------------------------------------------------*

           ADD 1 TO ACU-LIDOS.

           IF REG-EVSA2208-SALDO       GREATER ZEROS
               MOVE REG-EVSA2208       TO REG-SVSA2208
               WRITE REG-SVSA2208
               ADD 1                   TO ACU-GRAVADOS
               COMPUTE ACU-TOTAL-GRAVA = ACU-TOTAL-GRAVA +
                                                    REG-EVSA2208-SALDO
           ELSE
               ADD 1                   TO ACU-DESPREZADOS
               DISPLAY REG-EVSA2208
           END-IF.

           PERFORM 0130-LER-DADOS.

       0200-PROCESSAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       0300-FINALIZAR                  SECTION.
      *----------------------------------------------------------------*

           IF ACU-LIDOS GREATER ZEROS
               DISPLAY "=====ESTATISTICA===== "
               DISPLAY "===================== "
               DISPLAY "TOTAL DE REGISTROS LIDOS......: " ACU-LIDOS
               DISPLAY "TOTAL DE REGISTROS GRAVADOS...: " ACU-GRAVADOS
               DISPLAY "TOTAL REGISTROS DESPREZADOS...: "
                                                       ACU-DESPREZADOS
               DISPLAY "SALDO TOTAL REGISTROS GRAVADOS.: "
                                                ACU-TOTAL-GRAVA

           END-IF.

       0310-FECHAR-EVSA2208.

           CLOSE EVSA2208.

           IF FS-EVSA2208 NOT EQUAL ZEROS
               MOVE 'F2404P05'          TO WRK-PROGRAMA
               MOVE 'EVSA2208'          TO WRK-ARQUIVO
               MOVE '0310'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-EVSA2208         TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.



       0320-FECHAR-SVSA2208.

           CLOSE SVSA2208.

           IF FS-SVSA2208 NOT EQUAL ZEROS
               MOVE 'F2404P05'          TO WRK-PROGRAMA
               MOVE 'SVSA2208'          TO WRK-ARQUIVO
               MOVE '0320'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-SVSA2208         TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.

               GOBACK.

       0300-FINALIZAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       9999-TRATA-ERROS                SECTION.
      *----------------------------------------------------------------*

           CALL WRK-PROGRAMA-LOGERRO   USING WRK-DADOS.

           GOBACK.

       9999-TRATA-ERROS-FIM.           EXIT.
