      *================================================================*
       IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     F2404P06.
      *================================================================*
      *   PROGRAMA.....: F2404P06
      *   PROGRAMADOR..: RAFAEL DE OLIVEIRA
      *   ANALISTA.....: IVAN SANCHES
      *   DATA.........: 22/08/2024
      *   CONSULTORIA..: FOURSYS
      *----------------------------------------------------------------*
      *   OBJETIVO.....: CRIAR PROGRAMA QUE FACA LEITURA E GRAVE
      *                  OS SALDOS VALIDOS E NAO VALIDOS MOSTRANDO
      *                  ESTATISTICA.
      *
      *----------------------------------------------------------------*
      *-------------  ESPECIFICACOES DO PROGRAMA ----------------------*
      *   ARQUIVO......:
      *
      *   DDNAME.......:       TIPO                 BOOK
      *   CLIENTES             INPUT               BOOKCLI
      *   LANCAM               INPUT               BOOKLANC
      *   RELSAIDA             OUTPUT
      *----------------------------------------------------------------*
      *   INC'S........:
      *   BOOKMSG - VARIAVEIS PARA MENSAGEM DE ERRO
      *   BOOKLOG - AREA FORMATACAO DE ERROS -> GRAVALOG
      *
      *----------------------------------------------------------------*
      *   MODULOS......:                            BOOK....:
      *
      *   GRAVALOG                                  BOOKLOG
      *----------------------------------------------------------------*
      *----------------- ALTERACOES DO PROGRAMA------------------------*
      *   PROGRAMADOR..:
      *   ANALISTA.....:
      *   CONSULTORIA..:
      *   DATA.........:
      *   OBJETIVO.....:
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT                     DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION                   SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT CLIENTES ASSIGN TO CLIENTES
           FILE STATUS IS FS-CLIENTES.

           SELECT LANCAM ASSIGN TO LANCAM
           FILE STATUS IS FS-LANCAM.

           SELECT RELSAIDA ASSIGN TO RELSAIDA
           FILE STATUS IS FS-RELSAIDA.

      *----------------------------------------------------------------*

      *================================================================*
       DATA                            DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                            SECTION.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
      * INPUT - DADOS DO ARQUIVO DE ENTRADA (CLIENTES) LRECL = 035
      *----------------------------------------------------------------*

       FD  CLIENTES
           RECORDING MODE IS F.

       COPY BOOKCLI.

      *----------------------------------------------------------------*
      * OUTPUT - DADOS DO ARQUIVO DE SAIDA (LANCAM) LRECL = 016

      *----------------------------------------------------------------*

       FD  LANCAM
           RECORDING MODE IS F.

       COPY BOOKLANC.

      *----------------------------------------------------------------*
      * OUTPUT - DADOS DO ARQUIVO DE SAIDA (RELSAIDA) LRECL = 035

      *----------------------------------------------------------------*

       FD  RELSAIDA
           RECORDING MODE IS F.

       01  REG-RELSAIDA.
           05 REG-RELSAIDA-AGENCIA     PIC X(009).
           05 REG-RELSAIDA-NOME        PIC X(020).
           05 REG-RELSAIDA-SALDO       PIC 9(006).

      *----------------------------------------------------------------*
       WORKING-STORAGE                 SECTION.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- INICIO DA WORKING -----'.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEL DE FILE STATUTS -----'.
      *----------------------------------------------------------------*

       01  FS-CLIENTES                 PIC 9(002)          VALUE ZEROS.
       01  FS-LANCAM                   PIC 9(002)          VALUE ZEROS.
       01  FS-RELSAIDA                 PIC 9(002)          VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS   ACUMULADORA ----'.
      *----------------------------------------------------------------*

       01  ACU-LIDOS-CLIENTES          PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-LIDOS-LANCAM            PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-GRAVADOS                PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-DESPREZADOS             PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-TOTAL-SALDO-LANCAM      PIC 9(008) COMP-3   VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS DO BOOK-----'.
      *----------------------------------------------------------------*

       COPY BOOKLOG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- MENSAGENS DE ERRO-----'.
      *----------------------------------------------------------------*

       COPY BOOKMSG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- FIM DA WORKING -----'.
      *----------------------------------------------------------------*

      *================================================================*
       PROCEDURE                       DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       0000-PRINCIPAL                  SECTION.
      *----------------------------------------------------------------*

           PERFORM 0100-INICIALIZAR.

           PERFORM 0145-TESTAR-VAZIO.

           PERFORM 0200-PROCESSAR UNTIL FS-CLIENTES NOT EQUAL 00
                                  AND   FS-LANCAM   NOT EQUAL 00.

           PERFORM 0300-FINALIZAR.

       0000-PRINCIPAL-FIM.              EXIT.

      *----------------------------------------------------------------*
       0100-INICIALIZAR                 SECTION.
      *----------------------------------------------------------------*

           OPEN INPUT  CLIENTES.

           OPEN INPUT  LANCAM.

           OPEN OUTPUT RELSAIDA.

           PERFORM 0110-TESTAR-STATUS.


       0100-INICIALIZAR-FIM.           EXIT.

      *----------------------------------------------------------------*
       0110-TESTAR-STATUS              SECTION.
      *----------------------------------------------------------------*

       0120-TESTAR-CLIENTES.

           IF FS-CLIENTES NOT EQUAL ZEROS
              MOVE 'F2404P06'          TO WRK-PROGRAMA
              MOVE 'CLIENTES'          TO WRK-ARQUIVO
              MOVE '0120'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-CLIENTES         TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.
       0120-TESTAR-CLIENTES-FIM. EXIT.

       0130-TESTAR-LANCAM.

           IF FS-LANCAM NOT EQUAL ZEROS
              MOVE 'F2404P06'          TO WRK-PROGRAMA
              MOVE 'LANCAM'            TO WRK-ARQUIVO
              MOVE '0130'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-CLIENTES         TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.
       0130-TESTAR-LANCAM-FIM. EXIT.

       0140-TESTAR-RELSAIDA.
           IF FS-RELSAIDA NOT EQUAL ZEROS
              MOVE 'F2404P06'          TO WRK-PROGRAMA
              MOVE 'RELSAIDA'          TO WRK-ARQUIVO
              MOVE '0140'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-CLIENTES         TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.
       0140-TESTAR-RELSAIDA-FIM. EXIT.

       0110-TESTAR-STATUS-FIM.         EXIT.

      *----------------------------------------------------------------*
       0145-TESTAR-VAZIO               SECTION.
      *----------------------------------------------------------------*

           PERFORM 0160-LER-CLIENTES.

           IF FS-CLIENTES EQUAL 10
              DISPLAY "ARQUIVO CLIENTES VAZIO"
              DISPLAY "FIM DO PROGRAMA"
              PERFORM 0300-FINALIZAR
           END-IF.

           PERFORM 0170-LER-LANCAM.

           IF FS-LANCAM EQUAL 10
              DISPLAY "ARQUIVO LANCAM VAZIO"
              DISPLAY "FIM DO PROGRAMA"
              PERFORM 0300-FINALIZAR
           END-IF.

       0145-TESTAR-VAZIO-FIM.          EXIT.

      *----------------------------------------------------------------*
       0150-LER-DADOS                   SECTION.
      *----------------------------------------------------------------*

       0160-LER-CLIENTES.

            READ CLIENTES.

            IF FS-CLIENTES NOT EQUAL ZEROS AND FS-CLIENTES NOT EQUAL 10
                MOVE 'F2404P06'        TO WRK-PROGRAMA
                MOVE 'CLIENTES'        TO WRK-ARQUIVO
                MOVE '0160'            TO WRK-SECAO
                MOVE WRK-ERRO-LEITURA  TO WRK-MENSAGEM
                MOVE FS-CLIENTES       TO WRK-STATUS
                PERFORM 9999-TRATA-ERROS
           END-IF.

       0170-LER-LANCAM.

            READ LANCAM.

            IF FS-LANCAM  NOT EQUAL ZEROS AND FS-LANCAM NOT EQUAL 10
                MOVE 'F2404P06'        TO WRK-PROGRAMA
                MOVE 'LANCAM'          TO WRK-ARQUIVO
                MOVE '0170'            TO WRK-SECAO
                MOVE WRK-ERRO-LEITURA  TO WRK-MENSAGEM
                MOVE FS-LANCAM         TO WRK-STATUS
                PERFORM 9999-TRATA-ERROS
            END-IF.

                IF FS-LANCAM EQUAL 10
                    MOVE HIGH-VALUES   TO REG-LANCAM-AGCONTA
                END-IF.

       0150-LER-DADOS-FIM.             EXIT.


      *----------------------------------------------------------------*
       0200-PROCESSAR                  SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE

           WHEN REG-CLIENTES-AGCONTA   EQUAL REG-LANCAM-AGCONTA

               PERFORM 0210-SALDO-ATUALIZADO
               ADD 1                   TO    ACU-LIDOS-LANCAM

               PERFORM 0170-LER-LANCAM

           WHEN REG-CLIENTES-AGCONTA   LESS  REG-LANCAM-AGCONTA

               MOVE  REG-CLIENTES      TO    REG-RELSAIDA
               ADD   ACU-TOTAL-SALDO-LANCAM
                                       TO REG-RELSAIDA-SALDO

               WRITE REG-RELSAIDA

               ADD 1                   TO    ACU-GRAVADOS
               ADD 1                   TO    ACU-LIDOS-CLIENTES

               PERFORM 0160-LER-CLIENTES

           WHEN OTHER

               DISPLAY "CHAVE ERRADA "        REG-LANCAM
               ADD 1                   TO    ACU-DESPREZADOS

               PERFORM 0170-LER-LANCAM

           END-EVALUATE.

       0200-PROCESSAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       0210-SALDO-ATUALIZADO           SECTION.
      *----------------------------------------------------------------*

           IF REG-LANCAM-TIPO          EQUAL "C"
              ADD REG-LANCAM-SALDO     TO ACU-TOTAL-SALDO-LANCAM
           ELSE
              SUBTRACT REG-LANCAM-SALDO FROM REG-CLIENTES-SALDO
                                        GIVING ACU-TOTAL-SALDO-LANCAM
           END-IF.

       0210-SALDO-ATUALIZADO-FIM.      EXIT.
      *----------------------------------------------------------------*
       0250-ESTATISTICA                SECTION.
      *----------------------------------------------------------------*

           IF ACU-LIDOS-CLIENTES GREATER ZEROS
               DISPLAY "======ESTATISTICA======"
               DISPLAY "-----------------------"
               DISPLAY "REGISTROS GRAVADOS...: " ACU-GRAVADOS
               DISPLAY "LANCAMENTOS LIDOS....: " ACU-LIDOS-LANCAM
               DISPLAY "CLIENTES  LIDOS......: " ACU-LIDOS-CLIENTES
               DISPLAY "REGISTROS DESPREZADOS: " ACU-DESPREZADOS
           END-IF.

       0250-ESTATISTICA-FIM.            EXIT.

      *----------------------------------------------------------------*
       0300-FINALIZAR                  SECTION.
      *----------------------------------------------------------------*

           PERFORM 0250-ESTATISTICA.

       0310-FECHAR-CLIENTES.

           CLOSE CLIENTES.

           IF FS-CLIENTES NOT EQUAL ZEROS
               MOVE 'F2404P06'          TO WRK-PROGRAMA
               MOVE 'CLIENTES'          TO WRK-ARQUIVO
               MOVE '0310'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-CLIENTES         TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.

       0320-FECHAR-LANCAM.

           CLOSE LANCAM.

           IF FS-LANCAM NOT EQUAL ZEROS
               MOVE 'F2404P06'          TO WRK-PROGRAMA
               MOVE 'LANCAM'            TO WRK-ARQUIVO
               MOVE '0320'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-LANCAM           TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.

       0330-FECHAR-RELSAIDA.

           CLOSE RELSAIDA.

           IF FS-RELSAIDA NOT EQUAL ZEROS
               MOVE 'F2404P06'          TO WRK-PROGRAMA
               MOVE 'RELSAIDA'          TO WRK-ARQUIVO
               MOVE '0330'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-LANCAM           TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.
           GOBACK.

       0300-FINALIZAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       9999-TRATA-ERROS                SECTION.
      *----------------------------------------------------------------*

           CALL WRK-PROGRAMA-LOGERRO   USING WRK-DADOS.

           GOBACK.

       9999-TRATA-ERROS-FIM.           EXIT.
