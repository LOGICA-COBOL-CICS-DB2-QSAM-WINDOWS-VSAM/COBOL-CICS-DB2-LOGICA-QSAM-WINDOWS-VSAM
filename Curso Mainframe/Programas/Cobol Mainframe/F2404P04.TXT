      *================================================================*
       IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     F2404P04.
      *================================================================*
      *   PROGRAMA.....: F2404P04
      *   PROGRAMADOR..: RAFAEL DE OLIVEIRA
      *   ANALISTA.....: IVAN SANCHES
      *   DATA.........: 20/08/2024
      *   CONSULTORIA..: FOURSYS
      *----------------------------------------------------------------*
      *   OBJETIVO.....: DESENVOLVER UM PROGRAMA QUE FACA LEITURA
      *                  DE TODOS OS REGISTROS DO ARQUIVO ASSINA,
      *                  E MOSTRAR ESTATISTICA E MENSAGEM DE ERROS
      *----------------------------------------------------------------*
      *-------------  ESPECIFICACOES DO PROGRAMA ----------------------*
      *   ARQUIVO......:
      *
      *   DDNAME.......:       TIPO                 BOOK
      *   ASSINA               INPUT               BKASSINA
      *   RELSTR               INPUT
      *
      *----------------------------------------------------------------*
      *   INC'S........:
      *   BOOKMSG - VARIAVEIS PARA MENSAGEM DE ERRO
      *   BOOKLOG - AREA FORMATACAO DE ERROS -> GRAVALOG
      *
      *----------------------------------------------------------------*
      *   MODULOS......:                            BOOK....:
      *
      *   GRAVALOG                                  BOOKLOG
      *----------------------------------------------------------------*
      *----------------- ALTERACOES DO PROGRAMA------------------------*
      *   PROGRAMADOR..:
      *   ANALISTA.....:
      *   CONSULTORIA..:
      *   DATA.........:
      *   OBJETIVO.....:
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT                     DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION                   SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT ASSINA ASSIGN TO ASSINA
           FILE STATUS IS FS-ASSINA.

           SELECT RELSTR ASSIGN TO RELSTR
           FILE STATUS IS FS-RELSTR.
      *----------------------------------------------------------------*

      *================================================================*
       DATA                            DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                            SECTION.
      *----------------------------------------------------------------*
      * INPUT - DADOS DO ARQUIVO DE ENTRADA (ASSINA)  LRECL = 066
      *----------------------------------------------------------------*
       FD  ASSINA
           RECORDING MODE IS F.
       COPY BKASSINA.

      *----------------------------------------------------------------*
      * INPUT - DADOS DO ARQUIVO DE SAIDA (RELSTR) LRECL = 058

      *----------------------------------------------------------------*
       FD  RELSTR
           RECORDING MODE IS F.
       01  REG-RELSTR                  PIC X(058).

      *----------------------------------------------------------------*
       WORKING-STORAGE                 SECTION.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- INICIO DA WORKING -----'.
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEL DE FILE STATUTS -----'.
      *----------------------------------------------------------------*

       01  FS-ASSINA                   PIC 9(002)          VALUE ZEROS.
       01  FS-RELSTR                   PIC 9(002)          VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS   ACUMULADORA ----'.
      *----------------------------------------------------------------*

       01  ACU-LIDOS                   PIC 9(002) COMP-3   VALUE ZEROS.
       01  ACU-GRAVADOS                PIC 9(002) COMP-3   VALUE ZEROS.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEL DE APOIO ----'.
      *----------------------------------------------------------------*

       01  WRK-STREAMING               PIC X(010)          VALUE SPACES.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- VARIAVEIS DO BOOK-----'.
      *----------------------------------------------------------------*

       COPY BOOKLOG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- MENSAGENS DE ERRO-----'.
      *----------------------------------------------------------------*

       COPY BOOKMSG.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '----- FIM DA WORKING -----'.
      *----------------------------------------------------------------*

      *================================================================*
       PROCEDURE                       DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       0000-PRINCIPAL                  SECTION.
      *----------------------------------------------------------------*

           PERFORM 0100-INICIALIZAR.

           PERFORM 0140-TESTAR-VAZIO.

           PERFORM 0200-PROCESSAR UNTIL FS-ASSINA NOT EQUAL 00.

           PERFORM 0300-FINALIZAR.

       0000-PRINCIPAL-FIM.              EXIT.

      *----------------------------------------------------------------*
       0100-INICIALIZAR                 SECTION.
      *----------------------------------------------------------------*

           OPEN INPUT  ASSINA
                OUTPUT RELSTR.

           PERFORM 0110-TESTAR-STATUS.

       0100-INICIALIZAR-FIM.           EXIT.

      *----------------------------------------------------------------*
       0110-TESTAR-STATUS              SECTION.
      *----------------------------------------------------------------*

       0115-TESTAR-ASSINA.

           IF FS-ASSINA NOT EQUAL ZEROS
              MOVE 'F2404P03'          TO WRK-PROGRAMA
              MOVE 'ASSINA'            TO WRK-ARQUIVO
              MOVE '0115'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-ASSINA           TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.

       0120-TESTAR-RELSTR.

           IF FS-RELSTR NOT EQUAL ZEROS
              MOVE 'F2404P03'          TO WRK-PROGRAMA
              MOVE 'RELSTR'            TO WRK-ARQUIVO
              MOVE '0120'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE FS-ASSINA           TO WRK-STATUS
              PERFORM 9999-TRATA-ERROS
           END-IF.

       0110-TESTAR-STATUS-FIM.         EXIT.

      *----------------------------------------------------------------*
       0130-LER-DADOS                  SECTION.
      *----------------------------------------------------------------*

            READ ASSINA.

            IF FS-ASSINA NOT EQUAL ZEROS AND FS-ASSINA NOT EQUAL 10
                MOVE 'F2404P03'        TO WRK-PROGRAMA
                MOVE 'ASSINA'          TO WRK-ARQUIVO
                MOVE '0130'            TO WRK-SECAO
                MOVE WRK-ERRO-LEITURA  TO WRK-MENSAGEM
                MOVE FS-ASSINA         TO WRK-STATUS
                PERFORM 9999-TRATA-ERROS
            END-IF.

       0130-LER-DADOS-FIM.             EXIT.

      *----------------------------------------------------------------*
       0140-TESTAR-VAZIO               SECTION.
      *----------------------------------------------------------------*

           PERFORM 0130-LER-DADOS.

           IF FS-ASSINA EQUAL 10
              DISPLAY "ARQUIVO VAZIO"
              PERFORM 0300-FINALIZAR
           ELSE
              ACCEPT WRK-STREAMING
           END-IF.

       0140-TESTAR-VAZIO-FIM.          EXIT.

      *----------------------------------------------------------------*
       0200-PROCESSAR                  SECTION.
      *----------------------------------------------------------------*

           ADD 1 TO ACU-LIDOS.

           IF REG-STREAMING EQUAL WRK-STREAMING
               MOVE REG-ASSINA TO REG-RELSTR
               WRITE REG-RELSTR
               ADD 1 TO ACU-GRAVADOS
           END-IF.

      *    DISPLAY REG-ASSINA.

           PERFORM 0130-LER-DADOS.

       0200-PROCESSAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       0300-FINALIZAR                  SECTION.
      *----------------------------------------------------------------*

           IF ACU-LIDOS GREATER ZEROS
               DISPLAY "=====ESTATISTICA===== "
               DISPLAY "===================== "
               DISPLAY "TOTAL DE REGISTROS LIDOS: "    ACU-LIDOS
               DISPLAY "TOTAL DE REGISTROS GRAVADOS "  ACU-GRAVADOS
           END-IF.


       0310-FECHAR-ASSINA.

           CLOSE ASSINA.

           IF FS-ASSINA NOT EQUAL ZEROS
               MOVE 'F2404P03'          TO WRK-PROGRAMA
               MOVE 'ASSINA'            TO WRK-ARQUIVO
               MOVE '0310'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-ASSINA           TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.



       0320-FECHAR-RELSTR.

           CLOSE RELSTR.

           IF FS-RELSTR NOT EQUAL ZEROS
               MOVE 'F2404P03'          TO WRK-PROGRAMA
               MOVE 'RELSTR'            TO WRK-ARQUIVO
               MOVE '0320'              TO WRK-SECAO
               MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
               MOVE FS-RELSTR           TO WRK-STATUS
               PERFORM 9999-TRATA-ERROS
           END-IF.

               GOBACK.

       0300-FINALIZAR-FIM.             EXIT.

      *----------------------------------------------------------------*
       9999-TRATA-ERROS                SECTION.
      *----------------------------------------------------------------*

           CALL WRK-PROGRAMA-LOGERRO   USING WRK-DADOS.

           GOBACK.

       9999-TRATA-ERROS-FIM.           EXIT.
